###############################################################################
#
# hotkey-template-executable.eagle --
#
# Extensible Adaptable Generalized Logic Engine (Eagle)
# Hot-Key Template Type Script
#
# Copyright (c) 2007-2012 by Joe Mistachkin.  All rights reserved.
#
# See the file "license.terms" for information on usage and redistribution of
# this file, and for a DISCLAIMER OF ALL WARRANTIES.
#
# RCS: @(#) $Id: $
#
###############################################################################

proc hotkey-template-executable {} {
  package require HotKey.Template.Common

  try {
    set result null
    set thread [createTemplateThread result]

    set busyId [hotkey busy true [appendArgs \
        "Busy, please wait... (" [info level [info level]] )]]

    try {
      set code [$thread Send {
        lappend auto_path [info binary]
        set HotKey.Enterprise_NoThread true
        package require HotKey.Enterprise
        package require HotKey.Template.Common

        #
        # NOTE: Does the user want to filter the selection list?
        #
        set filter [hotkey yesno \
            "Filter executables based on those \"well-known\" to Windows?"]

        #
        # NOTE: Get a list of all the executable files within the hot-key
        #       "root directory" (i.e. the directory where all executable
        #       file searches should be conducted).
        #
        set pattern [file join [hotkey root] *.exe]
        set list [findFilesRecursive $pattern]

        #
        # NOTE: Filter the raw list of executable file names based on the
        #       applications known to Windows (e.g. via the registry, etc).
        #
        if {$filter} then {
          set list [filterApplicationFileNames $list]
        }

        #
        # NOTE: There must be at least one file name to select; otherwise,
        #       raise an error.
        #
        if {[llength $list] > 0} then {
          hotkey selectitem -executable $list
        } else {
          error "no executables detected"
        }
      } result]
    } finally {
      hotkey busy false $busyId
    }

    if {$code ne "Ok"} then {
      error [getStringFromObjectHandle $result]
    }

    return [appendArgs \
        [info newline] "exec \"" [string map [list \\ \\\\] [file \
        nativename [getStringFromObjectHandle $result]]] "\" &" [info \
        newline]]
  } finally {
    if {[info exists thread]} then {
      exitTemplateThread $thread
      unset -nocomplain thread
    }
  }
}

try {
  hotkey-template-executable
} finally {
  rename hotkey-template-executable ""
}
